apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

checkstyle {
    configFile = file("$rootDir/build_config/checkstyle.xml")
    toolVersion = 8.2
}
checkstyleMain {
    source = fileTree(dir: 'src/main', include: '**/*.java')
}
checkstyleTest {
    source = fileTree(dir: 'src/test', include: '**/*.java')
}

task jarSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'source'
}

task testJar(type: Jar) {
    from sourceSets.test.output
    classifier = 'tests'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/sudiptosarkar/maven"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from components.java
            artifact jarSources
        }
        maven(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/" + rootProject.name + "-" + rootProject.version + ".pom")
    }
}

build {
    dependsOn testJar
    dependsOn install
}

test {
    testLogging {
        // showStandardStreams = true
        useJUnit()
        exceptionFormat = 'full'
        afterSuite { desc, result ->
            if (desc.className) { // will match the outermost suite
                println "\n Test Suite: " + desc.className
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)\n"
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport


